# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=zaproxy
pkgver=2.4.3
pkgrel=1
pkgdesc="Integrated penetration testing tool for finding vulnerabilities in web applications"
url="https://www.owasp.org/index.php/ZAP"
arch=('any')
license=('Apache')
depends=('java-runtime' 'ttf-font')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/zaproxy/zaproxy/releases/download/${pkgver}/ZAP_${pkgver}_Linux.tar.gz)
sha512sums=('ec28e3acd8eb71707984bd0cf55099bcec21dfaf4ce515b3d47573ea349d1935ad44cfc64e1fdd4bf09fa58fee6eb6229e1640e0c9d6a910fdbbc0d4b6254ca2')

prepare() {
  cd ZAP_${pkgver}
  cat > ${pkgname} <<EOF
#!/bin/sh
cd /usr/share/zaproxy
./zap.sh
cd -
EOF
}

package() {
  cd ZAP_${pkgver}
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -pR . "${pkgdir}/usr/share/${pkgname}"
  install -Dm 755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
